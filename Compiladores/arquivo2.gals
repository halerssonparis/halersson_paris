#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
Package = compiler
ScannerCaseSensitive = true
ScannerTable = Compact
Input = String
Parser = SLR
#RegularDefinitions
variable: [_a-zA-Z][a-zA-Z0-9_]*
numbers: [0-9]+
//arithimetic_char: (+ | \- | \/ | *)
relational_operators: (<|>|<=|>=|!=|==)
logical_operators: (&&|\|\||!)
bitwise: (<<|>>|&|\||~|\^)
comment_multi_line: /\*[^\*]*\**([^/\*][^\*]*\*+)?/
comment_single_line: (/)/.*
hexa_decimal: [0-9A-F]x[0-9A-Fa-f]*
words: \"[^\n\"]* (\\\")* \"
char: '.'
#Tokens
numbers: {numbers}
variable : {variable}
binary: [0-1]+

number_float: {numbers}\.{numbers}
hexa_decima: {hexa_decimal}

: [\s\t\n\r]

int = variable : "int"
float = variable : "float"
string = variable : "string"
char = variable: "char"
byte = variable : "byte"
unsigned = variable : "unsigned"
long = variable: "long"
bool = variable: "bool"
void = variable: "void"
true = variable: "true"
false = variable: "false"

do = variable: "do"
if = variable: "if"
else = variable : "else"
else_if = variable : "elseif"
while = variable : "while"
for = variable : "for"
return = variable : "return"
break = variable : "break"
switch = variable : "switch"
case = variable : "case"

//aritmeticos
"+"
"-"
"*"
"/"
"%"
//relacional
">"
"<"
">="
"<="
"="
"!="
//logicos
"&&"
"||"
"!"
// bitwise
"~"
">>"
"<<"
"^"
"|"
"&"
// others
"("
")"
"{"
"}"
"["
"]"
";"
","
"."
"=>"
":"
"->"
"++"
"--"
"=="
"'"

end_line : ";"
echo = variable : "echo"
put = variable : "put"

comment_line: {comment_single_line}
comment_multi_lines: {comment_multi_line}
w: {words}
l_char: {char}
#NonTerminals
<INIT>
<__FUNCTION__>
<TYPE>

<PARAMS_TYPE>
<PARAMS_LIST>
<ID_PARAMS>

<LAMBDA>

<CMD_LIST>
<CMD_LIST2>
<CMD_LIST3>

<COMMENTS>

<ASSIGN>

<VARIABLE_DECLARATION>
<VAR_LIST>
<ID_TYPES>

<ID_TYPES2>

<REPEAT_LOOP>
<DO_LOOP>
<FOR_LOOP>
<WHILE_LOOP>

<LOOP_DEC_LIST>
<LOOP_COND_LIST>
<LOOP_ATRIB_LIST>
<LOOP_ATRIB_TYPE>

<IF>
<ELSE_IF>
<ELSE>

<DATA_ENTRY>
<TYPE_ENTRY>
<T>
<V>
//CODIGO DO PROFESSOR
<exp>
<exp1>
<exp2>
<exp3>
<exp4>
<exp5>
<exp6>
<exp7>
<exp8>
<exp9>
<exp10>
<exp11>
<exp12>
<exp13>
<op_rel>
<op_neg>
<op_bitwise>
<op_arit_baixa>
<op_arit_alta>
<op_arit_mais_alta>
//TERMINO DO CODIGO DO P.
#Grammar
//PROGRAMA INICIA COM UMA FUNCAO 
<INIT> ::= "{" <__FUNCTION__> "}" #200;
<__FUNCTION__> ::= <__FUNCTION__> <TYPE> variable#2 "(" <PARAMS_TYPE> ")"#8 "{" <CMD_LIST> "}"#9 | î;

<TYPE> ::= int #1 | float #1 | string #1 | char #1 | byte #1 | unsigned #1 | long #1 | bool #1 | void #1;

//PARAMENTROS (ISSO É PARA INICIAR FUNCOES) 
<PARAMS_TYPE> ::= #3 <ID_PARAMS> <PARAMS_LIST> | î #4;
<PARAMS_LIST> ::= "," <ID_PARAMS> <PARAMS_LIST> | î;
<ID_PARAMS> ::= #5 <TYPE> <ID_TYPES>  | <LAMBDA>;

//lambda retorna qualquer <TYPE>, é pra ser generico mesmo
<LAMBDA> ::= "(" <PARAMS_TYPE> ")" "=>" "{" <CMD_LIST> "}";

//CORPO DA FUNCAO
<CMD_LIST> ::=  <VARIABLE_DECLARATION>#10 ";" <CMD_LIST> | #15<REPEAT_LOOP> <CMD_LIST> |  #15<IF> <CMD_LIST> | <CMD_LIST2>;
<CMD_LIST2> ::= <DATA_ENTRY> <CMD_LIST> | return <exp> #160 ";" <CMD_LIST> |  <COMMENTS> <CMD_LIST> | <CMD_LIST3>;
<CMD_LIST3> ::= <ID_TYPES2> <ASSIGN>";" <CMD_LIST> | variable"(" <exp11> #151")" ";" <CMD_LIST> | î;

<COMMENTS> ::= comment_multi_lines | comment_line;
//BLOCOS DE INSTRUCAO
<ASSIGN> ::= "=" <exp> #100;
 

//DECLARACAO DE VARIAVEL
<VARIABLE_DECLARATION> ::= <TYPE> <VAR_LIST> | <TYPE> <VAR_LIST> #110 <ASSIGN>;
<VAR_LIST> ::= <VAR_LIST> "," <ID_TYPES> | <ID_TYPES>; //| <ASSIGN> | <VAR_LIST><ASSING>;
<ID_TYPES> ::= variable #6 | variable #7 "[" <exp> #99 #100"]";

<ID_TYPES2> ::=  variable #101 | variable #101 "[" <exp> #99 #100 "]" #102;


//LACOS DE REPETICOES
<REPEAT_LOOP> ::= <DO_LOOP> | <FOR_LOOP> | <WHILE_LOOP>;
<DO_LOOP> ::= do "{" <CMD_LIST> "}" while "(" <LOOP_COND_LIST> ")";
<FOR_LOOP> ::= for "(" <LOOP_DEC_LIST> ")" "{" <CMD_LIST> "}";
<WHILE_LOOP> ::= while "(" <LOOP_COND_LIST> ")" "{" <CMD_LIST> "}";

<LOOP_DEC_LIST> ::= <VARIABLE_DECLARATION> ";" <LOOP_DEC_LIST> | <LOOP_COND_LIST>;
<LOOP_COND_LIST> ::= <exp> <LOOP_ATRIB_LIST> | <exp> ";" <LOOP_COND_LIST> ;
<LOOP_ATRIB_LIST> ::= <LOOP_ATRIB_LIST> ";" <ID_TYPES2> <LOOP_ATRIB_TYPE> | î ; // <VAR_LIST> <ASSIGN>;
<LOOP_ATRIB_TYPE> ::= <ASSIGN> | "++" | "--";

//IF
<IF> ::= if  "(" <exp> ")" "{" <CMD_LIST> "}"#16 <ELSE_IF> <ELSE>;
<ELSE_IF> ::= #17 else_if  "(" <exp> ")" "{" <CMD_LIST> "}"#16 <ELSE_IF> | î;
<ELSE> ::= #18 else  "{" <CMD_LIST> "}"#16 | î;

//LEITURA E ESCRITA

<DATA_ENTRY> ::=  put variable #120 ";" | echo <V> <T> ";";
<T> ::= <T> "+" <V> |î;
<V> ::= <exp>;

//CODIGO DO PROFESSOR
<exp>  ::= <exp> "||" <exp1> #70| <exp1>;
<exp1> ::= <exp1> "&&" <exp2> #71| <exp2>;
<exp2> ::= <exp2> "|" <exp3> #72| <exp3>;
<exp3> ::= <exp3> "^" <exp4> #73| <exp4>;
<exp4> ::= <exp4> "&" <exp5> #74| <exp5>;
<exp5> ::= <exp5> <op_rel> <exp6> #82| <exp6>;
<exp6> ::= <exp6> <op_bitwise> <exp7> | <exp7>;
<exp7> ::= <exp7> <op_arit_baixa> <exp8> #80| <exp8>;
<exp8> ::= <exp8> <op_arit_alta> <exp9> #81| <exp9> ;
<exp9> ::= <op_neg> <exp10>  | <exp10>;
<exp10> ::= number_float #50| numbers #51 | binary #52| variable #53 | w #54 | l_char #56| true #55 | false #55 | "(" <exp> ")" | variable #57 "(" <exp11> #151")" | variable #53 "[" <exp> #99 #100"]"; //"["<exp>"]" | 
<exp11> ::= <exp12> | î;
<exp12> ::= <exp12> "," <exp13> | <exp13>;
<exp13> ::= <LAMBDA> | <exp> #150;

<op_rel> ::= ">" #84| "<" #84| ">=" #84| "<=" #84| "==" #84| "!=" #84; 
<op_neg> ::= "-"#83 | "~"#83 | "!"#83;
<op_bitwise> ::= "<<" | ">>";
<op_arit_baixa> ::= "+" #75 | "-" #76;
<op_arit_alta> ::= "*" #77| "/" #78 | "%" #78;

//Exemplo com tudo para ser usado no simulador
// -x + 8.8 - !x * 2 / ~y % 3 << 7 != y >> 3 || (x <= 3 && y > 8) | p & (h ^ g)
//TERMINO DO CODIGO DO PROFESSOR


